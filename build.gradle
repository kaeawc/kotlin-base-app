buildscript {
    ext.kotlin_version = "1.2.0-beta-31"
    ext.build_tools_version = "26.0.2"

    repositories {
        jcenter()
        google()
        // Uncomment to use Kotlin EAP builds
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.2" }
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://raw.githubusercontent.com/layerhq/releases-android/master/releases/" }
    }
    dependencies {
        classpath "org.jacoco:org.jacoco.core:0.7.9"
        classpath 'com.android.tools.build:gradle:3.0.0-beta7'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        // classpath 'com.google.gms:google-services:3.1.0'
        // classpath 'io.fabric.tools:gradle:1.23.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        // Uncomment to use Kotlin EAP builds
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.2" }
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
}

String supportLibraryVersion = "26.1.0"
String googlePlayVersion = "11.4.2"
String retrofitVersion = "2.3.0"
String leakCanaryVersion = "1.5.1"
String constraintLayoutVersion = "1.0.2"
String rxjavaVersion = "2.1.0"
String rxandroidVersion = "2.0.1"
String okhttpVersion = "3.9.0"
String daggerVersion = "2.11"
String timberVersion = "4.5.1"
String glideVersion = "4.2.0"
String multidexVersion = "1.0.2"

ext.deps = [

    kotlin: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
    kotlinReflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",

    // Android
    appCompat: "com.android.support:appcompat-v7:$supportLibraryVersion",
    design: "com.android.support:design:$supportLibraryVersion",
    cardview: "com.android.support:cardview-v7:$supportLibraryVersion",
    recyclerview: "com.android.support:recyclerview-v7:$supportLibraryVersion",
    constraintLayout: "com.android.support.constraint:constraint-layout:$constraintLayoutVersion",
    customTabs: "com.android.support:customtabs:$supportLibraryVersion",
    billing: "com.android.billingclient:billing:dp-1",

    // RxJava & RxAndroid
    rxJava: "io.reactivex.rxjava2:rxandroid:$rxandroidVersion",
    rxAndroid: "io.reactivex.rxjava2:rxjava:$rxjavaVersion",

    // HTTP
    okhttp: "com.squareup.okhttp3:okhttp:$okhttpVersion",
    okhttpMockWebserver: "com.squareup.okhttp3:mockwebserver:$okhttpVersion",
    retrofit: "com.squareup.retrofit2:retrofit:$retrofitVersion",
    moshi: "com.squareup.retrofit2:converter-moshi:$retrofitVersion",
    retrofitMock: "com.squareup.retrofit2:retrofit-mock:$retrofitVersion",
    retrofitRxJavaAdapter: "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion",

    // Network connectivity sampling
    connectionclass: 'com.facebook.network.connectionclass:connectionclass:1.0.1',

    // Categorization of Android devices
    yearclass: 'com.facebook.device.yearclass:yearclass:2.0.0',

    // Image Loading
    glide: "com.github.bumptech.glide:glide:$glideVersion",
    glideAnnotations: "com.github.bumptech.glide:compiler:$glideVersion",
    glideOkHttp: "com.github.bumptech.glide:okhttp3-integration:$glideVersion",
    glideTransformations: "jp.wasabeef:glide-transformations:3.0.1",

    // Dagger 2
    dagger: "com.google.dagger:dagger:$daggerVersion",
    daggerKapt: "com.google.dagger:dagger-compiler:$daggerVersion",
    daggerJavaXAnnotation: "org.glassfish:javax.annotation:10.0-b28",

    // Google Play Services
    firebaseMessaging: "com.google.firebase:firebase-messaging:$googlePlayVersion",
    locationServices: "com.google.android.gms:play-services-location:$googlePlayVersion",
    googleMaps: "com.google.android.gms:play-services-maps:$googlePlayVersion",
    googleAds: "com.google.android.gms:play-services-ads:$googlePlayVersion",

    // Facebook
    facebook: "com.facebook.android:facebook-android-sdk:4.26.0",
    bolts: "com.parse.bolts:bolts-android:1.4.0",

    // Chat + GCM
    layer: "com.layer.sdk:layer-sdk:0.23.21",

    // Crash Reporting + Metrics + Screen Capture
    appsee: "com.appsee:appsee-android:2.3.2",
    leanplum: 'com.leanplum:Leanplum:2.2.2',
    leanplumUiEditor: 'com.leanplum:UIEditor:2.2.2',
    crashlytics: "com.crashlytics.sdk.android:crashlytics:2.6.8@aar",

    // Link Tracking
    branch: "io.branch.sdk.android:library:2.12.0",

    // Logging
    timber: "com.jakewharton.timber:timber:$timberVersion",

    // Memory leak detection
    leakCanaryDebug: "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion",
    leakCanaryRelease: "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion",

    // Debugging
    stetho: "com.facebook.stetho:stetho:1.5.0",
    stethoOkHttp: "com.facebook.stetho:stetho-okhttp3:1.5.0",

    // Multidex Required for Older Android Versions
    multidex: "com.android.support:multidex:$multidexVersion",

    // Test dependencies
    junit: 'junit:junit:4.12',
    junitPlatform: "org.junit.platform:junit-platform-runner:1.0.1",
    mockitoForKotlin: "com.nhaarman:mockito-kotlin:1.5.0",
    robolectric: "org.robolectric:robolectric:3.4.2",

    // ImageView Zoom & Pan
    photoview: "com.github.chrisbanes:PhotoView:2.1.3",

    // Background Job Scheduling
    jobScheduler: "com.firebase:firebase-jobdispatcher:0.8.1",

    // Date parsing
    threeTen: "com.jakewharton.threetenabp:threetenabp:1.0.5"
]

task clean(type: Delete) {
    delete rootProject.buildDir
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}
